name=visual-studio-code
version=1.107.0
release=1
summary='Visual Studio Code'
group='Development/Tools'
desc='Visual Studio Code (vscode): Editor for building and debugging modern web and cloud applications (official binary version)'
maintainer='Vladimir Romanov <rirusha@altlinux.org>'
architectures=('amd64' 'arm64')
homepage='https://code.visualstudio.com/'
license=('custom')

appstream_app_id='com.visualstudio.code'

provides=('code' 'vscode')
conflicts=()

nonfree=1
nonfree_msgfile="MS-LICENSE.txt"

_pkg_arch() {
	if [ "${ARCH}" = "arm64" ]; then
		echo "${ARCH}"
	else
		echo "x64"
	fi
}

sources=(
	"https://raw.githubusercontent.com/microsoft/vscode/${version}/resources/linux/code.desktop?~name=code.desktop.in"
	"https://raw.githubusercontent.com/microsoft/vscode/${version}/resources/linux/code-url-handler.desktop?~name=code-url-handler.desktop.in"
	"https://raw.githubusercontent.com/microsoft/vscode/${version}/resources/linux/code-workspace.xml?~name=code-workspace.xml.in"
	"https://update.code.visualstudio.com/${version}/linux-$(_pkg_arch)/stable?~name=${name}_$(_pkg_arch)_${version}.tar.gz"
	"local:///${name}.sh"
)

checksums=(
	sha256:2f1782b30c4e040efff655fd9cf477930c5a0c81ddae27749b0cbb922c1d248e
	sha256:c361efa7e02fcad759ed80d2fbab67877f33219b981578af6fffaf18aeb12d9b
	sha256:3af748dd6578a1775e8eb7248ba397b7e11840df2ea6ee234ff76fee3dc306cf
	sha256:9cf13545117c297dce550eb187795c608a6bdad1d6acbc8450daceb6e06cbdbd
	sha256:8257a5ad82fa1f7dec11dfa064217b80df4cfec24f50cec7ca0ad62cf8295bfe
)

_set_meta_info() {
	sed 's/@@NAME_LONG@@/Visual Studio Code/g' "$1" |
		sed 's/@@NAME_SHORT@@/Code/g' |
		sed 's/@@NAME@@/code/g' |
		# https://altlinux.space/aides-pkgs/visual-studio-code/issues/20
		sed 's#@@EXEC@@#/usr/bin/code --enable-features=UseOzonePlatform --ozone-platform=wayland#g' |
		sed 's/@@ICON@@/visual-studio-code/g' |
		sed 's/@@URLPROTOCOL@@/vscode/g'
}

_pkg() {
	if [ "${ARCH}" = "arm64" ]; then
		echo 'VSCode-linux-arm64'
	else
		echo 'VSCode-linux-x64'
	fi
}

opt_deps=('git: For Source Control')
opt_deps_ru=('git: Для системы контроля версий')

deps=(
	libsecret
)

auto_req=1
auto_prov=1
auto_reqprov_method=dirty

prepare() {
	_set_meta_info "${srcdir}/code.desktop.in" >"${srcdir}/code.desktop"
	_set_meta_info "${srcdir}/code-url-handler.desktop.in" >"${srcdir}/code-url-handler.desktop"
	_set_meta_info "${srcdir}/code-workspace.xml.in" >"${srcdir}/code-workspace.xml"
}

package() {
	install -d "${pkgdir}/usr/share/licenses/${name}"
	install -d "${pkgdir}/opt/${name}"
	install -d "${pkgdir}/usr/bin"
	install -d "${pkgdir}/usr/share/applications"
	install -d "${pkgdir}/usr/share/icons"
	install -d "${pkgdir}/usr/share/mime/packages"

	install -m644 "${srcdir}/$(_pkg)/resources/app/LICENSE.rtf" "${pkgdir}/usr/share/licenses/${name}/LICENSE.rtf"
	install -m644 "${srcdir}/$(_pkg)/resources/app/resources/linux/code.png" "${pkgdir}/usr/share/icons/${name}.png"
	install -m644 "${srcdir}/code.desktop" "${pkgdir}/usr/share/applications/code.desktop"
	install -m644 "${srcdir}/code-url-handler.desktop" "${pkgdir}/usr/share/applications/code-url-handler.desktop"
	install -m644 "${srcdir}/code-workspace.xml" "${pkgdir}/usr/share/mime/packages/${name}-workspace.xml"
	install -Dm 644 "${srcdir}/$(_pkg)/resources/completions/bash/code" "${pkgdir}/usr/share/bash-completion/completions/code"
	install -Dm 644 "${srcdir}/$(_pkg)/resources/completions/zsh/_code" "${pkgdir}/usr/share/zsh/site-functions/_code"

	cp -r "${srcdir}/$(_pkg)/"* "${pkgdir}/opt/${name}"

	install -m755 "${srcdir}/${name}.sh" "${pkgdir}/usr/bin/code"
}

files() {
	files-find-binary code
	files-find \
		"/opt/**/*" \
		"/opt/**/.* " \
		"/usr/share/applications/*" \
		"/usr/share/bash-completion/completions/*" \
		"/usr/share/zsh/site-functions/*" \
		"/usr/share/icons/*" \
		"/usr/share/mime/packages/*" \
		"/usr/share/licenses/**/*"
}
